(in-package #:ld34)
(in-readtable :qtools)

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defclass player (sprite-entity hitbox-entity damageable-entity)
    ((v :initform (vec 0 0 0) :accessor v)
     (vmax :initform 15 :accessor vmax)
     (vacc :initform 2 :accessor vacc)
     (vdcc :initform 0.65 :accessor vdcc)
     (invincible :initform NIL :accessor invincible)
     (keymap :initform (make-hash-table :test 'eql) :accessor keymap))
    (:default-initargs
     :name :player
     :spritesheet "player"
     :health 10000
     :default-animation :idle)))

(defmethod initialize-instance :after ((player player) &key)
  (let ((keymap (keymap player)))
    (setf (gethash :left keymap) (q+:qt.key_left)
          (gethash :right keymap) (q+:qt.key_right)
          (gethash :up keymap) (q+:qt.key_up)
          (gethash :down keymap) (q+:qt.key_down)
          (gethash :light-punch keymap) (q+:qt.key_z)
          (gethash :heavy-punch keymap) (q+:qt.key_x)))
  (setf (animation player)
        (define-animation player
            (:file "player" :step (32 0) :duration 0.5)
          (:sequence :idle :offset (0 0)
           :frames (1 (2 :duration 1) 3 (4 :duration 1))))))

(defmethod key ((player player) key)
  (gethash key (keymap player)))

(defmethod update ((player player))
  (call-next-method)
  (with-slots-bound (player player)
    (let ((motion-input NIL))
      (when (key-pressed-p (key player :left))
        (decf (vx v) vacc)
        (setf motion-input T))
      (when (key-pressed-p (key player :right))
        (incf (vx v) vacc)
        (setf motion-input T))
      (when (key-pressed-p (key player :down))
        (decf (vy v) vacc)
        (setf motion-input T))
      (when (key-pressed-p (key player :up))
        (incf (vy v) vacc)
        (setf motion-input T))
      (cond ((<= (size-vec v) vdcc)
             (scale-vec v 0))
            ((not motion-input)
             (scale-vec v vdcc))))
    (when (<= vmax (size-vec v) vdcc)
      (scale-vec v (/ vmax (size-vec v))))
    (when invincible
      (with-timer-ready 'invincible 3
        (timer-ready-p 'invincible 3 (level))
        (setf invincible NIL)))
    (incf (vx (location player)) (vx v))
    (incf (vy (location player)) (vy v))
    (cap (level) (location player))))

(defmethod paint ((player player) target)
  (unless (and (invincible player) (timer-ready-p 'invincible-blink 0.1 (level)))
    (call-next-method)))
